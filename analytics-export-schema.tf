# ANALYTICS.export is a schema for holding tables/views (generated by dbt)
# that are for data to be exported to systems outside of Snowflake.
# Example:
# - data is published / syndicated to data products
# - data rollups for partnerships to consume via S3, FTP, or by email

# This export works in 2 ways:
# - a user with the ANALYTICS_ROLE can directly query tables/views in ANALYTICS.export,
#   load them into e.g. a Pandas dataframe
# - a user with the REPORT_ROLE can direct Snowflake to dump a table/view directly
#   into an S3 bucket using a Snowflake Storage Integration and Snowflake Stage

# In order to write directly to S3, you must have a file format created in ANALYTICS.export
# that indicates how you want to format the file (e.g. CSV or Parquet etc etc), and you 
# must grant access to the REPORT_ROLE to use that file format.
# Likewise you need to grant access to the REPORT_ROLE to the Snowflake stage you'll
# be usin to dump directly to S3.
# See https://docs.snowflake.com/en/user-guide/data-unload-s3.html


resource "snowflake_schema" "analytics_export_schema" {
  database = "ANALYTICS"
  name     = "EXPORT"

  is_transient        = false
  is_managed          = false
  data_retention_days = 1
}

resource "snowflake_schema_grant" "create_analytics_file_format_grant" {
  database_name = snowflake_schema.analytics_export_schema.database
  schema_name   = snowflake_schema.analytics_export_schema.name

  privilege              = "CREATE FILE FORMAT"
  roles                  = ["ACCOUNTADMIN"]
  with_grant_option      = false
  enable_multiple_grants = true
  depends_on             = [snowflake_schema.analytics_export_schema]
}

resource "snowflake_schema_grant" "create_analytics_stage_grant" {
  database_name = snowflake_schema.analytics_export_schema.database
  schema_name   = snowflake_schema.analytics_export_schema.name

  privilege              = "CREATE STAGE"
  roles                  = ["ACCOUNTADMIN"]
  with_grant_option      = false
  enable_multiple_grants = true
  depends_on             = [snowflake_schema.analytics_export_schema]
}

resource "snowflake_schema_grant" "analytics_export_owner_grant" {
  database_name = "ANALYTICS"
  schema_name   = "EXPORT"

  privilege              = "OWNERSHIP"
  roles                  = ["TRANSFORM_ROLE"]
  with_grant_option      = false
  enable_multiple_grants = true
  depends_on             = [snowflake_schema.analytics_export_schema]
}

resource "snowflake_schema_grant" "analytics_export_usage_grant" {
  database_name = "ANALYTICS"
  schema_name   = "EXPORT"

  privilege              = "USAGE"
  roles                  = ["REPORT_ROLE", "ANALYTICS_ROLE"]
  with_grant_option      = false
  enable_multiple_grants = true
  depends_on             = [snowflake_schema.analytics_export_schema]
}

resource "snowflake_file_format" "export_csv_file_format" {
  name                         = "CSV_EXPORT"
  database                     = snowflake_schema.analytics_export_schema.database
  schema                       = snowflake_schema.analytics_export_schema.name
  format_type                  = "CSV"
  compression                  = "NONE"
  field_delimiter              = ","
  record_delimiter             = "\n"
  binary_format                = "HEX"
  escape                       = "NONE"
  encoding                     = "UTF8"
  escape_unenclosed_field      = "\\"
  field_optionally_enclosed_by = "NONE"
}

resource "snowflake_file_format_grant" "export_csv_file_format_usage_grant" {
  database_name    = snowflake_schema.analytics_export_schema.database
  schema_name      = snowflake_schema.analytics_export_schema.name
  file_format_name = snowflake_file_format.export_csv_file_format.name

  privilege         = "USAGE"
  roles             = ["REPORT_ROLE", "ANALYTICS_ROLE"]
  on_future         = false
  with_grant_option = false

  depends_on = [snowflake_file_format.export_csv_file_format]
}

resource "snowflake_file_format" "export_parquet_file_format" {
  name        = "PARQUET_EXPORT"
  database    = snowflake_schema.analytics_export_schema.database
  schema      = snowflake_schema.analytics_export_schema.name
  format_type = "PARQUET"
  compression = "AUTO"
}

resource "snowflake_file_format_grant" "export_parquet_file_format_usage_grant" {
  database_name    = snowflake_schema.analytics_export_schema.database
  schema_name      = snowflake_schema.analytics_export_schema.name
  file_format_name = snowflake_file_format.export_parquet_file_format.name

  privilege         = "USAGE"
  roles             = ["REPORT_ROLE", "ANALYTICS_ROLE"]
  on_future         = false
  with_grant_option = false

  depends_on = [snowflake_file_format.export_parquet_file_format]
}
